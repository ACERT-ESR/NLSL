c Version 1.6  8/12/94
c**********************************************************************
c       
c                       ==================
c                       SUBROUTINE : RDDAT
c                       ==================
c
c       This reads the parameter file generated by the program lbll
c       into the common block defined in eprdat.inc. Returns nonzero
c       ierr if there was a read error. Ierr is returned as -100 
c       if the routine did not detect the proper version identification
c       string.
c
c       Notes:
c               Care should be taken to ensure that the common 
c               block definition in the include file eprdat.inc
c               is consistent with the information read in from the
c               disk files by this routine to avoid corrupting 
c               the parameter file.
c
c       written by DJS 15-NOV-87
c       modified by DEB 13-MAR-92 to allow longer filenames
c                       22-OCT-92 to check version i.d. string
c
c       Includes:
c               stdio.inc
c               eprdat.inc
c
c       Uses:
c
c**********************************************************************
c
      subroutine rddat(prname,lth,ierr)
c
      include 'stdio.inc'
      include 'eprdat.inc'
c
c Note size of verID must agree with version.inc:
c
      character prname*30,eprID*5,verID*4
      integer i,j,lth,ierr
c
      include 'version.inc'
c
c######################################################################
c
c----------------------------------------------------------------------
c     open input parameter file
c----------------------------------------------------------------------
c
      open (unit=ludisk,file=prname(:lth),access='direct',status='old',
     #     form='unformatted',recl=1024,iostat=ierr)
      if (ierr.eq.0) then
c
c *** new parameters are gib0,gib2,wxx,wyy,wzz
c
        read (ludisk,rec=1,iostat=ierr) eprID,verID,
     #     gxx,gyy,gzz,axx,ayy,azz,gamman,pl,pkxy,pkzz,b0,g0,a0,
     #     dx,dy,dz,tl,tkxy,tkzz,djf,oss,psi,ald,bed,gad,alm,bem,gam,
     #     cgtol,shiftr,shifti,fieldi,fieldf,djfprp,gib0,gib2,btol,
     #     (faa(i),i=1,5),(fgm(i),i=1,5),(fam(1,i),fam(2,i),i=1,5),
     #     (fgd(1,i),fgd(2,i),i=1,5),(fad(1,i),fad(2,i),i=1,5)

        if (eprID.ne.'EPRLL' .or. verID.ne.version) then
          ierr=-100
          return
        end if
c
c     New parameters in version 1.6 are kmn and mmn
c
        read (ludisk,rec=2,iostat=ierr)
     #     (cxp(i),i=1,6),((cpot(i,j),j=1,5),i=1,5),
     #     ((xlk(i,j),j=1,5),i=1,5),wxx,wyy,wzz,w0,
     #     (fwm(i),i=1,5),(fwd(1,i),fwd(2,i),i=1,5),
     #     mpl,mpkxy,mpkzz,in2,ipdf,ist,ipt,lptmx,kptmx,
     #     lband,kband,itd,ipsi0,lemx,lomx,kmx,kmn,mmx,mmn,ipnmx,
     #     ldelta,kdelta,jkmn,jmmn,nstep,ndim,neltot,itype,nfield,
     #     ((ixp(i,j),j=1,2),i=1,6),nelre,nelim,nort,ideriv
      end if
c
c----------------------------------------------------------------------
c     close parameter file
c----------------------------------------------------------------------
c
      close (unit=ludisk)
c
c----------------------------------------------------------------------
c     return to caller
c----------------------------------------------------------------------
c
      return
      end
